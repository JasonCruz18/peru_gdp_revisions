/********************
Efficiency Cross-Sectors
***

		Author
		---------------------
		Jason Cruz
		*********************/

		*** Program: efficiency_cross_sectors.do
		** 	First Created: 10/11/24
		** 	Last Updated:  10/11/24
			
	***
	** Just click on the "Run (do)" button, the code will do the rest for you.
	***

		
		
	/*----------------------
	Initial script configuration
	-----------------------*/

	cls 					// Clears the screen.
	clear all 				// Frees all memory.
	version 				// Displays the software version.
		
	set more off 			// Turns off pagination for output.
	cap set maxvar 12000	// Sets the maximum number of variables to 12000.
	program drop _all 		// Deletes all user-defined programs.
			
	capture log close 		// Closes the log file if open.
				
	pause on 				// Enables pauses in programs.
	set varabbrev off 		// Turns off variable abbreviation.
			
	//log using efficiency_tests.txt, text replace // Opens a log file and replaces it if it exists.

	

	/*----------------------
	Defining workspace path
	------------------------*/
	
	di `"Please, enter your path for storing the outputs of this dofile in the COMMAND WINDOW and press ENTER."'  _request(path)
	
	cd "$path"
		
		
		
	/*----------------------
	Setting folders to save outputs
	------------------------*/
		
	shell mkdir "output" 			// Creates folder to save outputs.
	//shell mkdir "output/graphs" 	// Creates folder to save graphs.
	shell mkdir "output/tables" 	// Creates folder to save tables.
	//shell mkdir "output/data" 		// Creates folder to save data.
		
	
	* Set as global vars
	
	//global graphs_folder "output/graphs"	// Use to export graphs.
	global tables_folder "output/tables"	// Use to export tables.
	//global data_folder "output/data"		// Use to export .dta.
	
		
		
	/*----------------------
	Import ODBC dataset and
	save temp
	-----------------------*/
		
		
	odbc load, exec("select * from sectorial_gdp_monthly_int_revisions_panel") dsn("gdp_revisions_datasets") lowercase sqlshow clear // Change frequency to monthly, quarterly or annual to load dataset from SQL. 
		
	
	save int_temp_panel_data, replace

	
	
	/*----------------------
	On-the-fly data
	cleaning
	-----------------------*/

	
	use int_temp_panel_data, clear


		* Order and sort
		
		sort vintages_date horizon // Key step to set both the ID and time vars for panel data.

		
		* At a glance (inspect data)

		d // Check entire dataset vars to understand its structure.
		sum // Summarize stats for all vars in the dataset (mean, standard deviation, etc.).
		count // Count the total number of observations, expected to be 6,696.

		
		* Fixing date format
		
		gen numeric_date = dofc(vintages_date) // To a Stata date in days.
		format numeric_date %td // To standard Stata date (e.g., day-month-year).

		gen target_date = mofd(numeric_date) // To a monthly date format.
		format target_date %tm // To standard Stata month (e.g., Jan 2023).

		drop vintages_date numeric_date // Drop the original vars since they are no longer needed.

		order target_date horizon // Reorder vars so that 'target_date' and 'horizon' appear first in the dataset.

		
		/* Definir la estructura de datos de panel */
		xtset target_date horizon
	
	
	save int_temp_panel_data_cleaned, replace
	

		
	/*----------------------
	Regression (revisions-cross-sectors [1/2])
	-----------------------*/

	* r^{GDP}_t(h) vs r^{sector_t(h-1)	 r^{sector}_t(h-2)
	*.........................................................................
	
	
	use int_temp_panel_data_cleaned, clear
	preserve // Save the current status
	
		* global
		
		global sectors agriculture fishing mining manufacturing electricity construction commerce services
	
		/* Limpiar cualquier estimación previa */
		estimates clear	
		
		/* Programa para procesar cada sector */
		program define process_cross_sector_r_gdp
			args sector model_type
			local dep_var r_gdp
			local indep_vars L1.r_`sector' L2.r_`sector'
			
			/* Ejecutar el modelo según el tipo (fe, re, xtscc) */
			if "`model_type'" == "fe" {
				xtreg `dep_var' `indep_vars', fe vce(cluster target_date)
			}
			else if "`model_type'" == "re" {
				xtreg `dep_var' `indep_vars', re vce(cluster target_date)
			}
			else if "`model_type'" == "xtscc_fe" {
				xtscc `dep_var' `indep_vars', fe
			}
			else if "`model_type'" == "xtscc_re" {
				xtscc `dep_var' `indep_vars', re
			}
			
			/* Obtener las dimensiones del panel con xtsum */
			xtsum `dep_var'
			scalar obs_between = r(n)     // Número de grupos (between)
			scalar obs_within = r(Tbar)   // Número de periodos promedio por grupo (within)
			scalar obs_total = r(N)       // Número total de observaciones (overall)
			
			/* Agregar los valores de observaciones a los resultados */
			estadd scalar n_`sector' obs_between
			estadd scalar h_`sector' obs_within
			estadd scalar N_`sector' obs_total
			
			/* Guardar los resultados del modelo */
			estimates store `model_type'_`sector'
		end

		/* Loop para correr las regresiones para cada sector */
		foreach sector of global sectors {
			
			/* Correr regresión de efectos fijos */
			process_cross_sector_r_gdp `sector' fe
			
			/* Test sobre las restricciones */
			test (L1.r_`sector' = 0) (L2.r_`sector' = 0)
			
			/* Guardar los valores de F y p */
			scalar chi_value = 2*r(F) // Don't report chi2
			scalar p_value = r(p)
			estadd scalar chi_`sector' chi_value
			estadd scalar p_`sector' p_value
			
			/* Correr regresión de efectos fijos con Driscoll-Kraay */
			process_cross_sector_r_gdp `sector' xtscc_fe

			/* Test sobre las restricciones */
			test (L1.r_`sector' = 0) (L2.r_`sector' = 0)
			
			/* Guardar los valores de F y p */
			scalar chi_value = 2*r(F) // Don't report chi2
			scalar p_value = r(p)
			estadd scalar chi_`sector' chi_value
			estadd scalar p_`sector' p_value

			/* Correr regresión de efectos aleatorios */
			process_cross_sector_r_gdp `sector' re
			
			/* Test sobre las restricciones */
			test (L1.r_`sector' = 0) (L2.r_`sector' = 0)
			
			/* Guardar los valores de F y p */
			scalar chi_value = r(chi2)
			scalar p_value = r(p)
			estadd scalar chi_`sector' chi_value
			estadd scalar p_`sector' p_value

			/* Correr regresión de efectos aleatorios con Driscoll-Kraay */
			process_cross_sector_r_gdp `sector' xtscc_re
			
			/* Test sobre las restricciones */
			test (L1.r_`sector' = 0) (L2.r_`sector' = 0)
			
			/* Guardar los valores de F y p */
			scalar chi_value = 2*r(F) // Alternatively e(chi2) 
			scalar p_value = r(p)
			estadd scalar chi_`sector' chi_value
			estadd scalar p_`sector' p_value
			
			/* Reportar los resultados usando esttab */
			esttab fe_`sector' xtscc_fe_`sector' re_`sector' xtscc_re_`sector' using "cross-sectors.tex", append ///
				b(%9.3f) se(%9.3f) stats(chi_`sector' p_`sector' n_`sector' h_`sector' N_`sector', label("Chi2" "p" "n" "h" "N") fmt(%9.3f %9.3f %9.0f %9.0f %9.0f)) ///
				order(_cons) ///
				varlabels(_cons "Intercepto" L.r_`sector' "`sector': r(-1)" L2.r_`sector' "`sector': r(-2)") ///
				noobs ///
				star(* 0.1 ** 0.05 *** 0.01) ///
				tex ///
				longtable
		}
	
	
	restore // Return to on-call status
	

	
	/*----------------------
	Regression (revisions-cross-sectors [2/2])
	-----------------------*/

	* r^{GDP}_t(h) vs r^{sector_{i}}_t(h-1) for i=agriculture, fishing, ...	
	*.........................................................................

	use int_temp_panel_data_cleaned, clear
	preserve // Save the current dataset state

		/* Global list of sectors */
		global sectors gdp agriculture fishing mining manufacturing electricity construction commerce services

		/* Clear previous estimates */
		estimates clear

		/* Define program to run regressions */
		program define regress_on_sectors
			args model_type
			local dep_var r_gdp  // Dependent variable
			local indep_vars     // Independent variables

			/* Create list of independent variables */
			foreach sector of global sectors {
				local indep_vars `indep_vars' L1.r_`sector'
			}

			/* Run the appropriate model based on the type */
			if "`model_type'" == "fe" {
				xtreg `dep_var' `indep_vars', fe vce(cluster target_date)
			}
			else if "`model_type'" == "re" {
				xtreg `dep_var' `indep_vars', re vce(cluster target_date)
			}
			else if "`model_type'" == "xtscc_fe" {
				xtscc `dep_var' `indep_vars', fe
			}
			else if "`model_type'" == "xtscc_re" {
				xtscc `dep_var' `indep_vars', re
			}

			/* Compute panel dimensions */
			xtsum `dep_var'
			scalar n_model = r(n)       // Groups (between)
			scalar h_model = r(Tbar)    // Periods/group (within)
			scalar N_model = r(N)       // Total observations (overall)

			/* Add panel dimensions to results */
			estadd scalar n_model = n_model
			estadd scalar h_model = h_model
			estadd scalar N_model = N_model

			/* Test restrictions */
			local restriction
			foreach sector of global sectors {
				local restriction `restriction' (L1.r_`sector' = 0)
			}
			test `restriction'

			/* Add F-statistic or Chi2 based on model type */
			if "`model_type'" == "re" {
				estadd scalar chi2_stat = r(chi2) // Report Chi2 for model (3)
			}
			else {
				estadd scalar chi2_stat = 2*r(F) // Report F-stat for all other models
			}
			
			/* Add p-value */
			estadd scalar p_val = r(p)

			/* Store the model */
			estimates store `model_type'
		end

		/* Run regressions for all models */
		local all_models
		foreach model_type in fe xtscc_fe re xtscc_re {
			regress_on_sectors `model_type'
			local all_models `all_models' `model_type'
		}

		/* Generate combined table with results */
		esttab `all_models' using "cross-all-sectors.tex", replace ///
			title("Cross-Sector Regressions") ///
			b(%9.3f) se(%9.3f) stats(chi2_stat p_val n_model h_model N_model, ///
				label("Chi2" "p-value" "n" "h" "N") ///
				fmt(%9.3f %9.3f %9.0f %9.0f %9.0f)) ///
			order(_cons) ///
			varlabels(_cons "Intercept") ///
			noobs ///
			star(* 0.1 ** 0.05 *** 0.01) ///
			tex ///
			longtable

	/* Restore the original dataset */
	restore
		
	
	
	/*----------------------
	Drop aux data and tables
	-----------------------*/	

		// List all .dta, .txt and .tex files in the current directory and store in a local macro
		local dta_files : dir . files "*.dta"
		local txt_files : dir . files "*.txt"
		local tex_files : dir . files "*.tex"

		// Iterate over each .dta file and delete it
		foreach file of local dta_files {
			erase "`file'"
		}	
		
		// Iterate over each .txt file and delete it
		foreach file of local txt_files {
			erase "`file'"
		}	
		
		// Iterate over each .tex file and delete it
		foreach file of local tex_files {
			erase "`file'"
		}	
		