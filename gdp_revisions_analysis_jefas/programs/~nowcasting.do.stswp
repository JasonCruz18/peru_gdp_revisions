/********************
Nowcasting GDP Revisions — EWMA
***

	Author
	---------------------
	D & J
	*********************/

	*** Program: nowcasting.do
	** 	First Created: 08/11/25
	** 	Last Updated:  08/25/25
		
***/


/*----------------------
Initial do-file setting
-----------------------*/

cls
clear all
version
set more off
cap set maxvar 12000
program drop _all
capture log close
pause on
set varabbrev off


/*----------------------
Defining workspace path
-----------------------*/

di `"Please, enter your path for storing the outputs of this dofile in the COMMAND WINDOW and press ENTER."'  _request(path)
cd "$path"


/*----------------------
Setting folders to save outputs
-----------------------*/

shell mkdir "input"
shell mkdir "input/data"
shell mkdir "output"
shell mkdir "output/tables"

global input_data "input/data"
global output_tables "output/tables"


/*----------------------
Time Series Analysis
-----------------------*/

cd "$input_data"
use e_gdp_revisions_ts, clear


/*----------------------
Clean-up at a glance
-----------------------*/

* Drop no longer needed vars (bench vars) for this forecasting analysis
drop bench_*

/*----------------------
EWS construction
-----------------------*/

tsset target_period, monthly
local delta = 0.5

	forvalues h = 1/12 {
		
		gen Y_ews_`h' = .
		quietly replace Y_ews_`h' = y_`h' in 1   // initialize with first nonmissing

		forvalues t = 2/`=_N' {
			* If y_h is not missing, update normally
			quietly replace Y_ews_`h' = `delta'*L1.Y_ews_`h' + y_`h' in `t' if !missing(y_`h') & !missing(L1.Y_ews_`h')
			   
			* If y_h is missing, just carry the exponential sum forward
			quietly replace Y_ews_`h' = L1.Y_ews_`h' in `t' if missing(y_`h')
		}

		* Burn first 12 obs
		*quietly replace Y_ews_`h' = . in 1/12
	}

	forvalues h = 2/12 {
		
		gen R_ews_`h' = .
		quietly replace R_ews_`h' = r_`h' in 1   // initialize with first nonmissing

		forvalues t = 2/`=_N' {
			* If y_h is not missing, update normally
			quietly replace R_ews_`h' = `delta'*L1.R_ews_`h' + r_`h' in `t' if !missing(r_`h') & !missing(L1.R_ews_`h')
			   
			* If y_h is missing, just carry the exponential sum forward
			quietly replace R_ews_`h' = L1.R_ews_`h' in `t' if missing(r_`h')
		}

		* Burn first 12 obs
		*quietly replace R_ews_`h' = . in 1/12
	}

	forvalues h = 3/12 {
		
		gen L1_R_ews_`h' = .
		quietly replace L1_R_ews_`h' = L1.R_ews_`h'

		* Burn first 12 obs
		*quietly replace L1_R_ews_`h' = . in 1/12
	}


* Save the dataset with EWMA for later merge
tempfile ewma
save `ewma'


/*----------------------
Omnibus regressions
-----------------------*/

* Keep common observations
qui {
    tsset target_period, monthly
    newey e_1 y_1 L1.e_1, lag(6) force
    predict residuals_aux, resid
}
keep if !missing(residuals_aux)
qui drop residuals_aux

forvalues h = 1/11 {
    
    if `h' == 1 {
        newey e_`h' L1.e_`h' y_`h', lag(6) force
        matrix b = e(b)
        gen alpha_`h' = b[1, "_cons"]
        gen theta_`h' = b[1, "y_`h'"]
        gen delta_`h' = b[1, "L1.e_`h'"]
    }
    
    else if `h' == 2 {
        newey e_`h' L1.e_`h' y_`h' r_`h', lag(6) force
        matrix b = e(b)
        gen alpha_`h' = b[1, "_cons"]
        gen theta_`h' = b[1, "y_`h'"]
        gen delta_`h' = b[1, "L1.e_`h'"]
        gen gamma_`h' = b[1, "r_`h'"]
    }
    
    else {
        newey e_`h' L1.e_`h' y_`h' r_`h' L1.r_`h', lag(6) force
        matrix b = e(b)
        gen alpha_`h' = b[1, "_cons"]
        gen theta_`h' = b[1, "y_`h'"]
        gen delta_`h' = b[1, "L1.e_`h'"]
        gen gamma_`h' = b[1, "r_`h'"]
        gen rho_`h'   = b[1, "L1.r_`h'"]
    }
    
}

tempfile coeffs
save `coeffs', replace


/*----------------------
Fitted values
-----------------------*/

forvalues h = 1/11 {
    gen e_hat_`h' = .
    
    if `h' == 1 {
        replace e_hat_`h' = (alpha_`h')/(1 - delta_`h') ///
                          + theta_`h'*Y_ews_`h'
    }
    else if `h' == 2 {
        replace e_hat_`h' = (alpha_`h')/(1 - delta_`h') ///
                          + theta_`h'*Y_ews_`h' ///
                          + gamma_`h'*R_ews_`h'
    }
    else {
        replace e_hat_`h' = (alpha_`h')/(1 - delta_`h') ///
                          + theta_`h'*Y_ews_`h' ///
                          + gamma_`h'*R_ews_`h' ///
                          + rho_`h'*L1_R_ews_`h'
    }
}

* Fitted y's (true GDP revisions forecast)
forvalues h = 1/11 {
    gen y_hat_`h' = y_`h' + e_hat_`h'
}

save "fitted_vals.dta", replace


/*----------------------
Forecast evaluation
-----------------------*/

use "fitted_vals.dta", clear


* Generate forecast errors
forvalues h = 1/11 {
gen fe_hat_`h' = y_`h' - y_hat_`h' // EWMA forecast error
gen fe_bench_`h' = e_`h' // Benchmark error (release vs. final)
}


* Loss differentials (squared errors)
forvalues h = 1/11 {
gen d_`h' = (fe_hat_`h')^2 - (fe_bench_`h')^2
}

cd "$path"
cd "$output_tables"
* Diebold–Mariano test using HAC variance
postfile results h dm_stat using "dm_results.dta", replace


forvalues h = 1/11 {
newey d_`h', lag(6) force
scalar dm_stat_`h' = _b[_cons] / _se[_cons]
di "DM test statistic for h=`h': " dm_stat_`h'
post results (`h') (dm_stat_`h')
}


postclose results



/*----------------------
Plots
-----------------------*/

use "fitted_vals.dta", clear
tsset target_period, monthly

twoway (tsline y_hat_1 y_1 y_12 if tin(2002m2,2012m12), cmissing(n)), ///
    title("Nowcast vs. true GDP growth — Horizon 1") ///
    legend(position(6) ring(1) rows(1)  ///
           label(1 "Nowcast (h=1)") ///
           label(2 "1st release") ///
           label(3 "True"))

	
twoway (tsline y_hat_1 y_1 y_12 if tin(2014m1,2020m2), cmissing(n)), ///
    title("Nowcast vs. true GDP growth — Horizon 1") ///
    legend(position(6) ring(1) rows(1)  ///
           label(1 "Nowcast (h=1)") ///
           label(2 "1st release") ///
           label(3 "True"))

twoway (tsline y_hat_2 y_2 y_12 if tin(2002m2,2012m12), cmissing(n)), ///
    title("Nowcast vs. true GDP growth — Horizon 2") ///
	legend(position(6) ring(1) rows(1)  ///
	   label(1 "Nowcast (h=2)") ///
	   label(2 "2nd release") ///
	   label(3 "True"))
	
twoway (tsline y_hat_2 y_2 y_12 if tin(2014m1,2020m2), cmissing(n)), ///
    title("Nowcast vs. true GDP growth — Horizon 2") ///
	legend(position(6) ring(1) rows(1)  ///
	   label(1 "Nowcast (h=2)") ///
	   label(2 "2nd release") ///
	   label(3 "True"))

twoway (tsline y_hat_3 y_3 y_12 if tin(2002m2,2012m12), cmissing(n)), ///
    title("Nowcast vs. true GDP growth — Horizon 3") ///
	legend(position(6) ring(1) rows(1)  ///
	   label(1 "Nowcast (h=3)") ///
	   label(2 "3rd release") ///
	   label(3 "True"))
	
twoway ///
    (tsline y_hat_3 if tin(2014m1,2020m2), cmissing(n) ///
        lcolor(blue) lwidth(0.5) lpattern(dash)) ///
    (tsline y_3 if tin(2014m1,2020m2), cmissing(n) ///
        lcolor(orange)) ///
    (tsline y_12 if tin(2014m1,2020m2), cmissing(n) ///
        lcolor(red) lpattern(solid)), ///
    title("Nowcast vs. true GDP growth — Horizon 3") ///
    legend(position(6) ring(1) rows(1) ///
           label(1 "Nowcast (h=3)") ///
           label(2 "3rd release") ///
           label(3 "True"))

	   
	   
twoway ///
    (tsline y_hat_3 if tin(2014m1,2020m2), cmissing(n) lcolor(red)) ///
    (tsline y_12     if tin(2014m1,2020m2), cmissing(n) lcolor(blue)), ///
    title("Nowcast vs. true GDP growth — Horizon 3") ///
    legend(position(6) ring(1) rows(1) ///
           label(1 "Nowcast (h=3)") ///
           label(2 "True"))
	   
twoway ///
    (tsline y_3 if tin(2014m1,2020m2), cmissing(n) lcolor(green)) ///
    (tsline y_12     if tin(2014m1,2020m2), cmissing(n) lcolor(blue)), ///
    title("Nowcast vs. true GDP growth — Horizon 3") ///
    legend(position(6) ring(1) rows(1) ///
           label(1 "3rd release") ///
           label(2 "True"))





